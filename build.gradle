import org.ajoberstar.grgit.*

buildscript {
    repositories {
        maven {
            url 'https://nexus.intr/repository/mavengroup/'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 5, 'minutes'
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:1.2")
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.3.9"
        classpath("ru.majordomo.gradle.plugins:copycompose:1.+")
        classpath("org.ajoberstar:gradle-git:1.6.0")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
    }
}
plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'
apply plugin: 'docker-compose'
apply plugin: 'ru.majordomo.gradle.plugins.copycompose'
apply plugin: 'maven-publish'
apply plugin: "com.gorylenko.gradle-git-properties"

def versionPropsFile = file('version.properties')
def versionBuild

if (versionPropsFile.canRead()) {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    versionBuild = versionProps['VERSION_BUILD'].toInteger()
} else {
    throw new GradleException("Could not read version.properties!")
}

ext.autoIncrementBuildNumber = {
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'ru.majordomo.hms.rc.user'
            artifactId 'resources'
            version '1.5.' + versionBuild
            artifact resourceJar
        }
    }
    repositories {
        maven {
            url uri(mavenUrl)
            credentials {
                username = mavenUser
                password = mavenPass
            }
        }
    }
}

version = '0.0.1-SNAPSHOT'

jar {
    baseName = 'rc-user'
    version = version
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into "static/docs"
    }
}

springBoot {
    buildInfo()
}

task resourceJar(type: Jar, dependsOn: compileJava) {
    baseName = 'resources'
    from sourceSets.main.output.classesDir
    include 'ru/majordomo/hms/rc/user/resources/**'
}

repositories {
    maven {
        url 'https://nexus.intr/repository/mavengroup'
    }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
    compile.exclude module: "hystrix-core"
    compile.exclude module: "feign-hystrix"
}

dependencies {
    compile('ru.majordomo.hms.rc.user:resources:1+')
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    compile('org.springframework.cloud:spring-cloud-starter-feign')
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    compile("org.springframework.cloud:spring-cloud-starter-sleuth")
    compile("com.netshoes:spring-cloud-sleuth-amqp:0.8")
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-data-redis')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.4'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.4'
    compile('ru.majordomo.hms.personmgr:exceptions:1.0.13')
    compile("ru.majordomo.hms.rc.staff:resources:1.1.+")
    compile("dnsjava:dnsjava:2.1.7")
    compile("com.cronutils:cron-utils:5.0.5")
    compile("commons-validator:commons-validator:1.6")
    compile("com.googlecode.libphonenumber:libphonenumber:7.7.3")
    compile("com.jcraft:jsch:0.1.54")
    testCompile('com.github.fakemongo:fongo:2.1.0')
    testCompile('com.github.kstyrc:embedded-redis:0.6')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc:${springRestDocsVersion}")
    testCompile('org.apache.qpid:qpid-broker:6.0.4')
    testCompile('com.h2database:h2')
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile(group: 'com.mysql', name: 'management', version: '1.1.6') { exclude(module: 'jmxtools') }
    compile('org.hibernate:hibernate-validator:5.4.1.Final')
    compile('javax.el:javax.el-api:3.0.0')
    compile('org.glassfish.web:javax.el:2.2.4')
    compile('com.google.guava:guava:21.0')
    compile("net.javacrumbs.shedlock:shedlock-spring:0.10.0")
    compile("net.javacrumbs.shedlock:shedlock-provider-mongo:0.8.0")
    compile("org.jongo:jongo:1.3.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springVersion}"
    }
}

clean.doFirst {
    delete "${rootDir}/work/"
}

ext {
    snippetsDir = file("build/generated-snippets")
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir "src/main/asciidoc"
}

build.dependsOn("resourceJar")
publish.doFirst autoIncrementBuildNumber
apply from: "https://nexus.intr/repository/init/gradle-init/0.1/dockertask.gradle"
